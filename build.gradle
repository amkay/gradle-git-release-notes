def vJavaLang = '1.7'
def gradleDir = "${rootProject.rootDir}/gradle"

wrapper.gradleVersion = '2.7'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.amkay:gradle-gitflow:0.2.0'
        classpath 'org.ajoberstar:gradle-git:1.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'groovy'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.ajoberstar:grgit:1.4.1'
}

groovydoc {
    link "https://docs.gradle.org/${wrapper.gradleVersion}/groovydoc/", 'org.gradle'
    link 'http://ajoberstar.org/grgit/docs/groovydoc', 'org.ajoberstar.grgit'
}

targetCompatibility = vJavaLang
sourceCompatibility = vJavaLang

apply plugin: 'eclipse'
apply plugin: 'idea'

idea {
    module.excludeDirs += [
        file('bin'),        // containing build artifacts from Eclipse (if any)
        file('.settings'),  // containing Eclipse metadata
        file("gradle"),
        file('.gradletasknamecache'),
        file('gradlew'),
        file('gradlew.bat')
    ]

    project.vcs = 'Git'
}

apply plugin: 'com.github.amkay.gitflow'

ext.githubUser = 'amkay'

group "com.github.$githubUser"
description 'A Gradle plugin that extracts release notes from Git\'s commit history.'

apply from: "$gradleDir/groovydoc-defaults.gradle"

apply plugin: 'maven-publish'
apply from: "$gradleDir/maven-publish-defaults.gradle"

apply plugin: 'com.jfrog.bintray'
apply from: "$gradleDir/bintray-defaults.gradle"
apply from: "$gradleDir/bintray-github-defaults.gradle"

bintray.pkg {
    repo = 'gradle-plugins'

    licenses = ['Apache-2.0']

    labels = ['gradle', 'plugin', 'git', 'release notes', 'release-notes']
}

apply plugin: 'codenarc'
apply from: "$gradleDir/checks-defaults.gradle"

apply plugin: 'license'
apply from: "$gradleDir/license-defaults.gradle"

license {
    ext.name = 'Max KÃ¤ufer'
    ext.year = '2015'
}

apply plugin: 'org.ajoberstar.github-pages'
apply from: "$gradleDir/github-pages-defaults.gradle"

apply from: "$gradleDir/bump-version.gradle"
apply from: "$gradleDir/bump-version-in-readme.gradle"
apply from: "$gradleDir/bump-doc-version-in-readme.gradle"
apply from: "$gradleDir/bump-download-version-in-readme.gradle"
